map "http://research.balgrist.ch/fhir2sphn/StructureMap/EncounterToAdministrativeCase" = "EncounterToAdministrativeCase"

conceptmap "cm-encounter-admitSource" {
  prefix s = "http://fhir.ch/ig/ch-core/ValueSet/bfs-medstats-17-admitsource"
  prefix t = "http://snomed.info/id"
  s:1 == t:264362003        // Zuhause
  s:2 == t:264362003        // Zuhause mit SPITEX Versorgung
  s:3 == t:285201006        // Krankenheim, Pflegeheim
  s:4 == t:257652008        // Altersheim, andere sozialmed. Institutionen
  s:5 == t:702914003        // Psychiatrische Klinik, anderer Betrieb
  s:55 == t:702914003       // Psychiatrische Abteilung/Klinik, gleicher Betrieb
  s:6 == t:25731000087105   // anderes Krankenhaus (Akutspital) oder Geburtshaus
  s:66 == t:25731000087105  // Akutabteilung/-klinik, gleicher Betrieb
  s:7 == t:257656006        // Strafvollzugsanstalt
  s:8 == t:276339004        // *andere -> Environment
  s:83 == t:702916001       // Rehabilitationsklinik, anderer Betrieb
  s:84 == t:702916001       // Rehabilitations- abteilung/-klinik, gleicher Betrieb
  s:9 == t:276339004        // *unbekannt -> Environment
}

conceptmap "cm-encounter-dischargedestination" {
  prefix s = "http://fhir.ch/ig/ch-core/ValueSet/bfs-medstats-28-dischargedestination"
  prefix t = "http://snomed.info/id"
  s:1 == t:264362003        // Zuhause
  s:2 == t:285201006        // Krankenheim, Pflegeheim
  s:3 == t:257652008        // Altersheim, andere sozialmed. Institution
  s:4 == t:702914003        // Psychiatrische Klinik, anderer Betrieb
  s:44 == t:702914003       // Psychiatrische Abteilung/Klinik, gleicher Betrieb
  s:5 == t:702916001        // Rehabilitationsklinik, anderer Betrieb
  s:55 == t:702916001       // Rehabilitations- abteilung/-klinik, gleicher Betrieb
  s:6 == t:25731000087105   // anderes Krankenhaus (Akutspital) oder Geburtshaus
  s:66 == t:25731000087105  // Akutabteilung/-klinik, gleicher Betrieb
  s:7 == t:257656006        // Strafvollzugsanstalt
  s:8 == t:276339004        // *andere -> Environment
  s:9 == t:276339004        // *unbekannt -> Environment
  s:0 == t:276339004        // *Todesfall -> Environment
}

conceptmap "cm-encounter-class" {
  prefix s = "http://terminology.hl7.org/ValueSet/encounter-class"
  prefix t = "http://snomed.info/id"
  s:IMP == t:394656005
  s:AMB == t:371883000
}


uses "http://hl7.org/fhir/StructureDefinition/Encounter" alias Encounter as source

uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-Content" alias Content as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-AdministrativeCase" alias AdministrativeCase as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-Location" alias Location as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-Admission" alias Admission as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-Discharge" alias Discharge as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-CareHandling" alias CareHandling as target
uses "http://research.balgrist.ch/fhir2sphn/StructureDefinition/SPHN-Concept" alias Concept as target

imports "http://research.balgrist.ch/fhir2sphn/StructureMap/Utils"


// Encounter -> Content.AdministrativeCase
group adminCase(source encounter : Encounter, target content: Content) <<types>> {
  encounter -> content.AdministrativeCase = create('AdministrativeCase') as case then {
    encounter.id as id -> case.id = ('Encounter/' & %id);
    encounter.meta as m then refSourceSystem(m, case);
    
    // period -> hasAdmission
    encounter.period as period -> case.hasAdmission = create('Admission') as admission, admission.id = uuid() then {
      period.start as start -> admission.hasDateTime = start;
      
      encounter.hospitalization as hospitalization then {
        // admitSource -> hasOriginLocation
        hospitalization.admitSource as admitSource then {
          admitSource.coding first as coding -> admission.hasOriginLocation = create('Location') as location then {
            coding.code as code -> location.id = uuid(), translate(code, '#cm-encounter-admitSource') as cm,
              location.hasTypeCode as typeCode, typeCode.termid = (%cm.code), typeCode.iri = (%cm.system & '/' & %cm.code) "adminSource_code";
          };
        };
      };
    };

    // class -> CareHandling
    encounter.class as class -> case.hasCareHandling = create('CareHandling') as care then {
      encounter.meta as m then refSourceSystem(m, care);
      class.code as code -> care.id = uuid(), translate(code, '#cm-encounter-class') as cm,
        care.hasTypeCode as typeCode, typeCode.termid = (%cm.code), typeCode.iri = (%cm.system & '/' & %cm.code) "class";
    };

    // period.stop -> hasDischarge
    encounter.period as period then {
      period.end as end -> case.hasDischarge = create('Discharge') as discharge, discharge.id = uuid() then {
        end -> discharge.hasDateTime = end "period.end";

        encounter.hospitalization as hospitalization then {
          // extension [dischargedestination] -> hasDischargeLocation
          hospitalization.extension as ext where $this.url = 'http://fhir.ch/ig/ch-core/StructureDefinition/ch-ext-bfs-ms-dischargedestination' -> discharge.hasTargetLocation = create('Location') as location then {
            ext.value : Coding as coding then {
              coding.code as code -> location.id = uuid(), translate(code, '#cm-encounter-dischargedestination') as cm,
                location.hasTypeCode as typeCode, typeCode.termid = (%cm.code), typeCode.iri = (%cm.system & '/' & %cm.code) "dischargedestination";
            };
          };
        };
      };
    };
  } "AdministrativeCase";
}
